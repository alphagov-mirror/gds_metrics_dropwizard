plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    mavenCentral()
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group = 'engineering.gds-reliability'

description = ' Library for prometheus instrumentation in Dropwizard based apps.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    configurations = ['archives']
    publish = true

    pkg {
        repo = 'maven'
        name = 'gds-metrics-dropwizard'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/gds_metrics_dropwizard.git'
    }
}

test {
    dependsOn << compileTestJava
    testLogging {
        events "skipped", "failed", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }

    baseName = 'gds-metrics-dropwizard'
}

dependencies {
    api 'io.prometheus:simpleclient_dropwizard:0.2.0'
    api 'io.prometheus:simpleclient_servlet:0.2.0'
    api 'io.prometheus:simpleclient_hotspot:0.2.0'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.glassfish.jersey.core:jersey-client:2.25.1'
    implementation'javax.servlet:javax.servlet-api:3.1.0'

    compileOnly 'io.dropwizard:dropwizard-core:1.3.5'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.3'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.3'
    testImplementation 'io.dropwizard:dropwizard-testing:1.2.3'
    testImplementation 'org.eclipse.jetty:jetty-servlet:9.4.8.v20171121'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
